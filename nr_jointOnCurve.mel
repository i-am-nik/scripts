/*
    Create joint chain on curve

    Script by : Nikhil Ramchandani (3D rigger)
    Connect with me on LinkedIn : https://www.linkedin.com/in/nikramchandani/

*/

global proc nr_jointOnCurveUI()
{
    int $jointAmount;
    string $jointsCreated[];
    string $result = `promptDialog
                        -title "Joints on curve"
                        -message "Enter no of joints:"
                        -button "OK" -button "Cancel"
                        -defaultButton "OK" -cancelButton "Cancel"
                        -dismissString "Cancel"`;

    // If the user presses OK in the prompt dialog, store the entered joint amount in the $jointAmount variable.
    if ($result == "OK") 
    {
        $jointAmount = `promptDialog -query -text`;
    }

    nr_jointOnCurve($jointAmount);   
}

global proc string[] nr_jointOnCurve(int $numJoint)
{
    // Get the selected curve.
    string $selectedCurve[] = `ls -sl`;
    string $jointName[];

    // Calculate the number of control vertices for the curve.
    int $numCV = `getAttr ($selectedCurve[0] + ".degree")` + `getAttr ($selectedCurve[0] + ".spans")`;
    int $smoothCV = $numCV * $numJoint;

    string $dupCurve[] = `rebuildCurve -ch 1 -rpo 0 -rt 0 -end 1 -kr 0 -kcp 0 -kep 1 -kt 0 -s $smoothCV -d 1 -tol 0.01 $selectedCurve[0]`;
    string $dupCurveShapeNode[] = `listRelatives -shapes $dupCurve[0]`;

    select -cl;

    // Loop through the number of joints specified by the user.
    for($i=0;$i<=$numJoint;$i++)
    {
        int $jointCV  = ($smoothCV/$numJoint) * $i;

        // Get the world space position of the CV.
        float $jointPos[] = `xform -q -t ($dupCurveShapeNode[0] + ".controlPoints[" + $jointCV + "]")`;

        // Create a joint at the world space position of the CV.
        $jointName[$i] = `joint -n ("joint" + $i) -p $jointPos[0] $jointPos[1] $jointPos[2]`;
    }

    delete $dupCurve;
    joint -e -ch -oj yzx -sao yup $jointName[0];
    select $jointName[0];


    // Return an array of the joint names created.
    return $jointName;
}

nr_jointOnCurveUI();