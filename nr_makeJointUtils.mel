 /*
    Make Joint Utils [1.0.2]

    Script by : Nikhil Ramchandani (3D rigger)
    Connect with me on LinkedIn : https://www.linkedin.com/in/nikramchandani/

    Run command :
    nr_makJointUtils_UI("");
*/

global proc string nr_makJointUtils_UI(string $args)
{
    if (`window -exists nr_makJointUtils_UI`)
    {
        deleteUI nr_makJointUtils_UI;
    }

    string $window = `window -title "makJointUtils" nr_makJointUtils_UI`;

    columnLayout -adjustableColumn true;
    button -label "Create Joint Tool" -command ("createJointTool()");

    separator -style "single" -height 10;
    button -aop 1 -label "Create joint on selection" - command ("createJointOnSelection()");
    button -aop 1 -label "Create a joint on center of selection" -command ("createJointOnCenter()");
    button -aop 1 -label "Create joint chain on selection" -command ("createJointChainOnSelection()");
    button -aop 1 -label "Create joint chain between two points" -command ("createJointBwTwoPoints()");

    separator -style "single" -height 10;
    button -aop 1 -label "Create locator" - command ("createLocator()");
    button -aop 1 -label "Create locator on selection" - command ("createLocatorOnSelection()");
    button -aop 1 -label "Create a locator on center of selection" -command ("createLocatorOnCenter()");

    separator -style "single" -height 10;
    button -aop 1 -label "Create Cluster" -command ("createCluster()");

    separator -style "single" -height 10;
    button -aop 1 -bgc 1 0 0 -label "Close" -command ("deleteUI -window " + $window);

    setParent ..;
    showWindow $window;

    return "Opened nr_makJointUtils";
}


global proc string createJointTool()
{
    string $createJointTool =`JointTool`;
    refresh;

    return $createJointTool;
}


global proc string[] createJointOnSelection()
{
    string $selection[] = `ls -sl`;
    string $jointName[];
    int $count = 0;

    select -cl;
    refresh;

    // Iterate through the selection
    for ($object in $selection)
    {
        // Get the position of the object
        float $pos[] = `xform -q -ws -t $object`;

        // Create a new joint at the object position
        $jointName[$count] = `joint -p $pos[0] $pos[1] $pos[2]`;

        //parent the joint to the world
        if(size(`listRelatives -p $jointName[$count]`) != 0)
        {
            parent -w $jointName[$count];
        }

        $count++;
    }

    return $jointName;
}


global proc string createJointOnCenter()
{
    string $selected[] = `ls -sl`; // Get the selected objects
    vector $centroid = <<0, 0, 0>>;
    int $count = size($selected);

    select -cl;
    refresh;

    // Loop through the selected objects and get their world space position
    for ($i = 0; $i < $count; $i++)
    {
        float $translation[] = `xform -q -ws -t $selected[$i]`;
        $centroid += <<$translation[0], $translation[1], $translation[2]>>;
    }

    // Calculate the centroid position
    $centroid /= $count;

    // Create a joint at the centroid position
    string $jointName = `joint -p ($centroid.x) ($centroid.y) ($centroid.z)`;

    return $jointName;
}

global proc string[] createJointChainOnSelection()
{
    string $selection[] = `ls -sl`;
    string $jointName[];
    int $count = 0;

    select -cl;
    refresh;

    // Iterate through the selection
    for ($object in $selection)
    {
        // Get the position of the object
        float $pos[] = `xform -q -ws -t $object`;

        // Create a new joint at the object position
        $jointName[$count] = `joint -p $pos[0] $pos[1] $pos[2]`;
        $count++;
    }

    return $jointName;
}

global proc string[] createJointBwTwoPoints()
{
    float $jointAmount;
    string $result = `promptDialog
                    -title "Create Joints"
                    -message "Enter no of joints:"
                    -button "OK" -button "Cancel"
                    -defaultButton "OK" -cancelButton "Cancel"
                    -dismissString "Cancel"`;

    if ($result == "OK") 
    {
        $jointAmount = `promptDialog -query -text`;
    }

    string $selection[] = `ls -selection`;
    float $inc = (1/float($jointAmount));

    select -cl;

    for($i=0;$i<=$jointAmount;$i++)
    {
        string $jointName = `joint -n ("joint" + $i)`;

        string $tempConstraintName = $jointName + "parentConstraint";
        parentConstraint -n $tempConstraintName $selection[0] $selection [1] $jointName;
    
        setAttr ($tempConstraintName + "."  + $selection[0] + "W0") (0 + ($inc * $i));
        setAttr ($tempConstraintName + "."  + $selection[1] + "W1") (1 - ($inc * $i));

        delete $tempConstraintName;
    }

    return {""};
}


global proc string createLocator()
{
    // creating a locator at origin
    string $createLocatorTool = `spaceLocator -p 0 0 0`;

    select -cl;
    refresh;

    return $createLocatorTool;
}


global proc int createLocatorOnSelection()
{
    string $selection[] = `ls -sl`;
    int $count = 0;

    // Iterate through the selection
    for ($object in $selection)
    {
        // Get the position of the object
        float $pos[] = `xform -q -ws -t $object`;
        
        select -cl;
        refresh;

        // Create a new locator at the object position
        spaceLocator -n ("newlocator" + $count) -p $pos[0] $pos[1] $pos[2];
        
        // transferring local position to world position
        for($axis in {"X","Y","Z"})
        {
            setAttr ("newlocator" + $count + ".translate" + $axis) `getAttr ("newlocator" + $count + "Shape.localPosition" + $axis)`;
            setAttr ("newlocator" + $count + "Shape.localPosition" + $axis) 0;
        }

        $count++;
    }

    return $count;
}


global proc string createLocatorOnCenter()
{
    string $selected[] = `ls -sl`; // Get the selected objects
    vector $centroid = <<0, 0, 0>>;
    int $count = size($selected);

    select -cl;
    refresh;

    // Loop through the selected objects and get their world space position
    for ($i = 0; $i < $count; $i++)
    {
        float $translation[] = `xform -q -ws -t $selected[$i]`;
        $centroid += <<$translation[0], $translation[1], $translation[2]>>;
    }

    // Calculate the centroid position
    $centroid /= $count;

    // Create a locator at the centroid position
    spaceLocator - n "centroidLocator" -p ($centroid.x) ($centroid.y) ($centroid.z);
        
    // transferring local position to world position
    for($axis in {"X","Y","Z"})
    {
        setAttr ("centroidLocator.translate" + $axis) `getAttr ("centroidLocatorShape.localPosition" + $axis)`;
        setAttr ("centroidLocatorShape.localPosition" + $axis) 0;
    }

    return "";
}

global proc string[] createCluster()
{
    // creating a cluster on selected object
    string $createCluster[] = `cluster`;

    return $createCluster;
}